
options {
  LOOKAHEAD = 1;
  IGNORE_CASE = true;
  STATIC = false;
  USER_TOKEN_MANAGER=false;
  USER_CHAR_STREAM=false;

  // JJTree options
  MULTI=true;
  NODE_DEFAULT_VOID = true;
}

PARSER_BEGIN(JSONSSParser)

package org.metadatacenter.jsonss.parser;

import org.metadatacenter.jsonss.core.settings.ReferenceTypeDirectiveSetting;
import org.metadatacenter.jsonss.core.settings.ShiftDirectiveSetting;
import org.metadatacenter.jsonss.core.settings.EmptyLiteralValueDirectiveSetting;
import org.metadatacenter.jsonss.core.settings.EmptyCellLocationDirectiveSetting;

import java.util.*;

public class JSONSSParser 
{
  public JSONSSParser(java.io.InputStream stream, int dum)
  {
    this(stream, null);
  }
}

PARSER_END(JSONSSParser)

/* White space */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* Comments */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

TOKEN: { <JSON_STRING: "string" > }
TOKEN: { <JSON_NUMBER: "number" > }
TOKEN: { <JSON_BOOLEAN: "boolean" > }

// JSON Tokens

TOKEN: { <OPEN_PAREN: "("> }
TOKEN: { <CLOSE_PAREN: ")"> }
TOKEN: { <OPEN_BRACE: "{"> }
TOKEN: { <CLOSE_BRACE: "}"> }
TOKEN: { <OPEN_BRACKET: "["> }
TOKEN: { <CLOSE_BRACKET: "]"> }
TOKEN: { <COMMA: ","> }
TOKEN: { <COLON: ":"> }

// JSON-SS Tokens

TOKEN: { <CELL_WILDCARD: "*"> }
TOKEN: { <EXCLAMATION_POINT: "!"> }
TOKEN: { <EQUAL: "="> }
TOKEN: { <AT: "@"> }

TOKEN: { <LOCATION: "Location"> }

TOKEN: { <TRUE: "true" | "TRUE">}
TOKEN: { <FALSE: "false" | "FALSE">}
TOKEN: { <NULL: "null" > }

TOKEN: { <NO_SHIFT: "NoShift"> }
TOKEN: { <SHIFT_UP: "ShiftUp"> }
TOKEN: { <SHIFT_DOWN: "ShiftDown"> }
TOKEN: { <SHIFT_LEFT: "ShiftLeft"> }
TOKEN: { <SHIFT_RIGHT: "ShiftRight"> }

TOKEN: { <ERROR_IF_EMPTY_LOCATION: "ErrorIfEmptyLocation"> }
TOKEN: { <WARNING_IF_EMPTY_LOCATION: "WarningIfEmptyLocation"> }
TOKEN: { <SKIP_IF_EMPTY_LOCATION: "SkipIfEmptyLocation"> }
TOKEN: { <PROCESS_IF_EMPTY_LOCATION: "ProcessIfEmptyLocation"> }

TOKEN: { <PROCESS_IF_EMPTY_LITERAL: "ProcessIfEmptyLiteral"> }
TOKEN: { <ERROR_IF_EMPTY_LITERAL: "ErrorIfEmptyLiteral"> }
TOKEN: { <WARNING_IF_EMPTY_LITERAL: "WarningIfEmptyLiteral"> }
TOKEN: { <SKIP_IF_EMPTY_LITERAL: "SkipIfEmptyLiteral"> }

TOKEN: { <DEFAULT_LOCATION_VALUE: "DefaultLocationValue"> }
TOKEN: { <DEFAULT_LITERAL_VALUE: "DefaultLiteralValue"> }

TOKEN: { <TO_UPPER_CASE: "toUpperCase"> }
TOKEN: { <TO_LOWER_CASE: "toLowerCase"> }
TOKEN: { <TRIM: "trim"> }
TOKEN: { <CAPTURING: "capturing"> }
TOKEN: { <REVERSE: "reverse"> }
TOKEN: { <REPLACE: "replace"> }
TOKEN: { <REPLACE_ALL: "replaceAll"> }
TOKEN: { <REPLACE_FIRST: "replaceFirst"> }
TOKEN: { <APPEND: "append"> }
TOKEN: { <PREPEND: "prepend"> }

TOKEN: {
        <STRING: "\"" ( <ALLOWABLE_CHARACTERS> )* "\"" >
    |   <ALLOWABLE_CHARACTERS:(   
	        (~["\"", "\\", "\u0000"-"\u001f"])
	           | ("\\"
	            ( ["u"] ["0"-"9","a"-"f", "A"-"F"] ["0"-"9","a"-"f", "A"-"F"] ["0"-"9","a"-"f", "A"-"F"] ["0"-"9","a"-"f", "A"-"F"]
	                | ["\"", "\\", "b", "f", "n", "r", "t"]
	                )
	            )
	        )
	    >
}

TOKEN:
{
  <QUOTED_NAME_LITERAL: "'" (~["'"])* ("''" (~["'"])*)* "'" >
|
  <NUMBER:  <INTEGER> | <INTEGER> <FRACTIONAL_DIGITS> | <INTEGER> <EXPONENT> | <INTEGER> <FRACTIONAL_DIGITS> <EXPONENT> >
|
  <INTEGER: ("-") ? ( <DIGIT> | <NONZERO_DIGIT> <DIGITS>) >
|
  <FRACTIONAL_DIGITS: "." <DIGITS> >
|
  <#EXP: ["e", "E"] ( "+" | "-" )? >
|
  <EXPONENT: <EXP> <DIGITS> >
|
  <DIGITS: ( <DIGIT> )+ >
|
  <#LETTER: [ "a"-"z", "A"-"Z" ]>
|
  <#DIGIT: ["0"-"9"] >
| 
  <#NONZERO_DIGIT: ["1"-"9"] >
| 
  <LOCATION_SPECIFICATION: (<LETTER> | <DIGIT> | "_" | "-" | "#" | "." | ":" | "/" | "*" )+ | "'" (~["'"])* "'"> 
}

SimpleNode json_expression() #JSONExpression : {}
{
  ( json_object() | json_array() )
  {
     return jjtThis;
  }
}
void json_value() #JSONValue: {}
{
    (  json_object()
     | json_array()
     | json_boolean()
     | json_string()
     | json_number()
     | json_null()
     | jsonss_reference()
    )
}

void json_array() #JSONArray : {}
{
  <OPEN_BRACKET> ( json_value() ( <COMMA> json_value() )* )? <CLOSE_BRACKET>
}

void json_object() #JSONObject : {}
{
   <OPEN_BRACE> ( json_key_value_pair() ( <COMMA> json_key_value_pair() )* )? <CLOSE_BRACE>
}

void json_key_value_pair() #JSONKeyValuePair: {}
{
  json_string() <COLON> json_value()
}

void json_boolean() #JSONBoolean: 
{
   boolean b;
}
{
  (   <TRUE> { b = Boolean.TRUE; }
    | <FALSE> { b = Boolean.FALSE; }
  )
  {
    jjtThis.b = b;
  }
}

void json_string() #JSONString: 
{
  Token t;
}
{
  t = <STRING> 

  {
    String s = t.image;
    jjtThis.s = s.substring(1, s.length() - 1);
  }
}

void json_number() #JSONNumber:
{
  Token t;
  Number n;
}
{
  t=<NUMBER>
        {
            try {
              n = Integer.valueOf(t.image);

            }
            catch (NumberFormatException nfe1) {
                try {
                    n = Long.valueOf(t.image);
                }
                catch (NumberFormatException nfe2) {
                    try {
                        n = Float.valueOf(t.image);
                    }
                    catch (NumberFormatException nfe3) {
                        try {
                            n = Double.valueOf(t.image);
                        }
                        catch  (NumberFormatException nfe4) {
                            n = Double.NaN;
                        }
                    }
                }

            }
        }
    {
      jjtThis.n = n;
    }
}

void json_null() #JSONNull : {}
{
  <NULL>
}

void jsonss_reference() #Reference : {}
{
   <AT> jsonss_reference_cell_location_specification()
       (
         <OPEN_PAREN>
            ( jsonss_reference_directives() )?  ( jsonss_value_extraction_function() )?
         <CLOSE_PAREN>
       )?
  {
  }
}

void jsonss_reference_directives(): {}
{
  jsonss_reference_directive() ( jsonss_reference_directive() )?
}

void jsonss_reference_directive(): {}
{
  (  jsonss_reference_type_directive()
   | jsonss_default_cell_location_value_directive() 
   | jsonss_default_literal_value_directive() 
   | jsonss_empty_literal_value_directive()
   | jsonss_empty_cell_location_directive()
   | jsonss_shift_directive()
  ) 
}

void jsonss_range_reference() #RangeReference : {}
{
   <AT> jsonss_reference_location_specification() <COLON> jsonss_reference_location_specification() 
       (
         <OPEN_PAREN>
            jsonss_range_reference_directive() ( jsonss_range_reference_directive() ) *
         <CLOSE_PAREN>
       )?
  {
  }
}

void jsonss_range_reference_directive(): {}
{
  (  jsonss_reference_type_directive()
   | jsonss_default_cell_location_value_directive() 
   | jsonss_default_literal_value_directive() 
   | jsonss_empty_literal_value_directive()
   | jsonss_empty_cell_location_directive()
   | jsonss_shift_directive()
  ) 
}

void jsonss_reference_cell_location_specification() #ReferenceCellLocationSpecification : 
{
  String source = null;
  String location = null;
  String literal = null;
  Token t;
}
{ 
  (
     ( ( source = jsonss_reference_source_name() <EXCLAMATION_POINT> )? ( location = jsonss_reference_location_specification()  ) )
   | literal = string_literal()
  )
  
  { 
     jjtThis.source = source;
     jjtThis.location = location;
     jjtThis.literal = literal;
  }
}

String jsonss_reference_source_name() : 
{
  Token t;
}
{
  t = <QUOTED_NAME_LITERAL> 

  {
    String s = t.image;
    return s.substring(1, s.length() - 1);
  }
}

String jsonss_reference_location_specification() :
{
  Token t;
}
{
  t = <LOCATION_SPECIFICATION>

  { return t.image; }
}

void jsonss_reference_type_directive() #ReferenceTypeDirective : 
{
  ReferenceTypeDirectiveSetting referenceTypeDirectiveSetting;
}
{
  (
       <JSON_STRING> { referenceTypeDirectiveSetting = ReferenceTypeDirectiveSetting.JSON_STRING; }
     | <JSON_NUMBER> { referenceTypeDirectiveSetting = ReferenceTypeDirectiveSetting.JSON_NUMBER; }
     | <JSON_BOOLEAN> { referenceTypeDirectiveSetting = ReferenceTypeDirectiveSetting.JSON_BOOLEAN; }
  ) 

  {
    jjtThis.referenceTypeDirectiveSetting = referenceTypeDirectiveSetting;
  }
}

void jsonss_empty_cell_location_directive() #EmptyCellLocationDirective :
{
  EmptyCellLocationDirectiveSetting emptyCellLocationDirectiveSetting;
}
{
  (
     <ERROR_IF_EMPTY_LOCATION> { emptyCellLocationDirectiveSetting = EmptyCellLocationDirectiveSetting.ERROR_IF_EMPTY_LOCATION; }
   | <WARNING_IF_EMPTY_LOCATION> { emptyCellLocationDirectiveSetting = EmptyCellLocationDirectiveSetting.WARNING_IF_EMPTY_LOCATION; }
   | <SKIP_IF_EMPTY_LOCATION> { emptyCellLocationDirectiveSetting = EmptyCellLocationDirectiveSetting.SKIP_IF_EMPTY_LOCATION; }
   | <PROCESS_IF_EMPTY_LOCATION> { emptyCellLocationDirectiveSetting = EmptyCellLocationDirectiveSetting.PROCESS_IF_EMPTY_LOCATION; }
  )

  {
    jjtThis.emptyCellLocationDirectiveSetting = emptyCellLocationDirectiveSetting;
  }
}

void jsonss_empty_literal_value_directive() #EmptyLiteralValueDirective :
{
  EmptyLiteralValueDirectiveSetting emptyLiteralValueDirectiveSetting;
}
{
  (
     <ERROR_IF_EMPTY_LITERAL> { emptyLiteralValueDirectiveSetting = EmptyLiteralValueDirectiveSetting.ERROR_IF_EMPTY_LITERAL; }
   | <WARNING_IF_EMPTY_LITERAL> { emptyLiteralValueDirectiveSetting = EmptyLiteralValueDirectiveSetting.WARNING_IF_EMPTY_LITERAL; }
   | <SKIP_IF_EMPTY_LITERAL> { emptyLiteralValueDirectiveSetting = EmptyLiteralValueDirectiveSetting.SKIP_IF_EMPTY_LITERAL; }
   | <PROCESS_IF_EMPTY_LITERAL> { emptyLiteralValueDirectiveSetting = EmptyLiteralValueDirectiveSetting.PROCESS_IF_EMPTY_LITERAL; }
  )

  {
    jjtThis.emptyLiteralValueDirectiveSetting = emptyLiteralValueDirectiveSetting;
  }
}

void jsonss_shift_directive() #ShiftDirective :
{
  ShiftDirectiveSetting shiftDirectiveSetting;
}
{
  (
     <SHIFT_UP> { shiftDirectiveSetting = ShiftDirectiveSetting.SHIFT_UP; }
   | <SHIFT_DOWN> { shiftDirectiveSetting = ShiftDirectiveSetting.SHIFT_DOWN; }
   | <SHIFT_LEFT> { shiftDirectiveSetting = ShiftDirectiveSetting.SHIFT_LEFT; }
   | <SHIFT_RIGHT> { shiftDirectiveSetting = ShiftDirectiveSetting.SHIFT_RIGHT; }
   | <NO_SHIFT> { shiftDirectiveSetting = ShiftDirectiveSetting.NO_SHIFT; }
  )

  {
    jjtThis.shiftDirectiveSetting = shiftDirectiveSetting;
  }
}

void jsonss_default_literal_value_directive() #DefaultLiteralValueDirective :
{
  String literalValue;
}
{
  <DEFAULT_LITERAL_VALUE> <EQUAL> literalValue = string_literal()

  {
    jjtThis.defaultLiteralValue = literalValue;
  }
}

void jsonss_default_cell_location_value_directive() #DefaultCellLocationValueDirective :
{
  String cellLocationValue;
}
{
  <DEFAULT_LOCATION_VALUE> <EQUAL> cellLocationValue = string_literal()

  {
    jjtThis.defaultCellLocationValue = cellLocationValue;
  }
}

void jsonss_value_extraction_function() #ValueExtractionFunction:
{
  Token t;
  int functionID;
}
{
  ((
    ( 
       t = <TO_UPPER_CASE> { functionID = t.kind; }
     | t = <TO_LOWER_CASE> { functionID = t.kind; }
     | t = <TRIM> { functionID = t.kind; }
     | t = <REVERSE> { functionID = t.kind; }
     | t = <CAPTURING> { functionID = t.kind; }
     | t = <REPLACE> { functionID = t.kind; }
     | t = <REPLACE_ALL> { functionID = t.kind; }
     | t = <REPLACE_FIRST> { functionID = t.kind; }
     | t = <APPEND> { functionID = t.kind; }
     | t = <PREPEND> { functionID = t.kind; }
    ) ( LOOKAHEAD(2) <OPEN_PAREN> jsonss_value_extraction_function_argument() ( <COMMA> jsonss_value_extraction_function_argument() )* <CLOSE_PAREN> )?
   )
   |
   ( <OPEN_BRACKET> ( LOOKAHEAD(2) jsonss_value_extraction_function_argument() ( <COMMA> jsonss_value_extraction_function_argument() )* )? <CLOSE_BRACKET> )
     { functionID = CAPTURING; } 
  )
  {
    jjtThis.functionID = functionID;
  }
}

void jsonss_value_extraction_function_argument() #ValueExtractionFunctionArgument : {}
{
  jsonss_reference() | string_literal()
}

String string_literal() #StringLiteral: 
{
  Token t;
}
{
  t = <STRING> 

  {
    String s = t.image;
    jjtThis. s = s.substring(1, s.length() - 1);
    return s.substring(1, s.length() - 1);
  }
}
